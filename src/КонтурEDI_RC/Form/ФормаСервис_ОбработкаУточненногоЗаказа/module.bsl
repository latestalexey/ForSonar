Перем НужноПеревыставитьЗаказ;

Процедура ПриОткрытии()
	
	ПриОткрытииФормы(ЭтаФорма);
	
	УстановитьВидимостьЭлементовУправления();
	
	НужноПеревыставитьЗаказ = Ложь;
	
	ОтклонятьОтветыНаЗаказСДобавленнымТоваром = ПолучитьКонстантуEDI("ОтклонятьОтветыНаЗаказСДобавленнымТоваром");
	
	ЕстьДобавленныеСтроки = Ложь;
	
	Для каждого Стр Из ТаблицаСравнения Цикл
		
		Если (Стр.КоличествоЗаказали = 0) ИЛИ (Стр.КоличествоПодтвердили > Стр.КоличествоЗаказали) Тогда
			
			ЕстьДобавленныеСтроки = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОтклонятьОтветыНаЗаказСДобавленнымТоваром = Истина И ЕстьДобавленныеСтроки Тогда
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Принять.КнопкаПоУмолчанию = Ложь;
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Принять.Доступность = Ложь;
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Отклонить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьЭлементовУправления()
	
	//скрываем видимость надписей и таблицу сравнения полей шапки сообщений
	Если ТаблицаСравненияШапкаСообщения.Итог("ЕстьРасхождения") = 0 Тогда
		
		ЭлементыФормы.НадписьШапкаСообщения.Видимость 			= Ложь;
		ЭлементыФормы.ТаблицаСравненияШапкаСообщения.Видимость 	= Ложь;
		ЭлементыФормы.НадписьТовары.Видимость 					= Ложь;
		
		ЭлементыФормы.ТаблицаСравнения.Верх 	= 8;
		ЭлементыФормы.ТаблицаСравнения.Высота 	= 369;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНажатииКнопки(Кнопка)
	
	Параметры = Новый Структура();
	Параметры.Вставить("Заказ",Заказ);
	Параметры.Вставить("ТаблицаСравнения",ТаблицаСравнения);
	
	ОбработкаСобытияПодключаемогоМодуля("ПриНажатииКнопки",, Новый Структура("ИмяФормы,ИмяКнопки,Параметры", "ФормаСервис_ОбработкаУточненногоЗаказа", Кнопка.Имя, Параметры));

КонецПроцедуры

Процедура ОсновныеДействияФормыПринять(Кнопка)
	
	ПриНажатииКнопки(Кнопка);

	Если НужноПеревыставитьЗаказ Тогда
		
		Если ПеревыставитьЗаказПоставщику(Заказ,ТаблицаСравнения) Тогда
			
			Параметры = Новый Структура();
			Параметры.Вставить("Статус","Отменить");
			
			ОтправитьСообщение("ORDERS",Заказ,Параметры);	
			
			ЭтаФорма.Закрыть();
			
		КонецЕсли;
		
	Иначе
		
		//ткпт
		ПеренестиПодтвержденноеКоличествоВДокумент(Заказ, ТаблицаСравнения);
		
		УстановитьСтатусДокумента(Заказ,"ЗаказУточненПринят","Заказ");
		
		ЭтаФорма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

//ткпт
Процедура ПеренестиПодтвержденноеКоличествоВДокумент(Документ, ТаблицаСравнения)
	
	Если ИмяКонфигурации1С = "ТКПТ" Тогда
		
		ДокументОбъект   = Документ.ПолучитьОбъект();
		ТабЧастьЗаказ = ДокументОбъект.Товары;
		Для Каждого СтрокаСравнения Из ТаблицаСравнения Цикл
			Если Не (СтрокаСравнения.КоличествоЗаказали = СтрокаСравнения.КоличествоПодтвердили) Тогда
				СтрокаТабЧастиЗаказа = ТабЧастьЗаказ.Найти(СтрокаСравнения.Номенклатура,"Номенклатура");
				Если Не СтрокаТабЧастиЗаказа = Неопределено Тогда
					СтрокаТабЧастиЗаказа.КоличествоФактическое = СтрокаСравнения.КоличествоПодтвердили;	
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ДокументОбъект.Записать();
		КонецПопытки;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОтклонить(Кнопка)
	
	Если Вопрос("Заказ будет отменен целиком, продолжить?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Подтверждение отмены заказа") = КодВозвратаДиалога.Да Тогда
		
		ПриНажатииКнопки(Кнопка);
	
		Параметры = Новый Структура();
		Параметры.Вставить("Статус","Отменить");
		Сообщение = ПодготовитьИсходящееСообщение("ORDERS", Заказ, Параметры);
		
		//не хватает ссылки на исходное сообщение
		//пока протяем ее из НайтиСообщениеДокумента
		СуществующееСообщение = НайтиСообщениеДокумента(Заказ,"ORDERS","Исходящее");
		
		Сообщение.Вставить("ПереотправляемоеСообщениеСсылка",СуществующееСообщение);
		ПараметрыОтправки = Новый Структура("ОтправитьСообщениеИзФормы,Сообщение",Истина,Сообщение);
		
		ОтправитьСообщение("ORDERS",Заказ,ПараметрыОтправки);
		
		//а что в этот момент происходит с ORDRSP от поставщика? По идее его надо аннулировать.
		ВходящийORDRSPСсылка	= НайтиСообщениеДокумента(Заказ,"ORDRSP","Входящее");
		ОбъектСообщенияORDRSP	= ПолучитьОбъектСообщения(ВходящийORDRSPСсылка);
		ОбъектСообщенияORDRSP.ТипСообщения = "#ORDRSP";
		СохранитьОбъектСообщения(ОбъектСообщенияORDRSP);//или флаг "Отклонен"?
		
		ЭтаФорма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаСравненияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ЦветКрасный = WebЦвета.СветлоРозовый;
	ЦветЖелтый	= WebЦвета.СветлоЗолотистый;
	
	Если ДанныеСтроки.КоличествоЗаказали = 0 Тогда
		
		ОформлениеСтроки.ЦветФона		= ЦветКрасный;
		ОформлениеСтроки.Ячейки.КоличествоЗаказали.УстановитьТекст("Не заказывали!");
		
	Иначе	
		
		Если НЕ ДанныеСтроки.Количество = ДанныеСтроки.КоличествоПодтвердили Тогда
			Если ДанныеСтроки.Количество = 0 Тогда
				ОформлениеСтроки.Ячейки.Количество.ЦветФона = ЦветКрасный;
			Иначе
				ОформлениеСтроки.Ячейки.Количество.ЦветФона = ЦветЖелтый;
			КонецЕсли;
		КонецЕсли;	
		
		Если НЕ ДанныеСтроки.КоличествоЗаказали = ДанныеСтроки.КоличествоПодтвердили Тогда
			Если ДанныеСтроки.КоличествоПодтвердили = 0 Тогда
				ОформлениеСтроки.Ячейки.КоличествоПодтвердили.ЦветФона = ЦветКрасный;
			Иначе
				ОформлениеСтроки.Ячейки.КоличествоПодтвердили.ЦветФона = ЦветЖелтый;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ДанныеСтроки.ЦенаБезНДСЗаказали = ДанныеСтроки.ЦенаБезНДСПодтвердили Тогда
			Если ДанныеСтроки.ЦенаБезНДСПодтвердили = 0 Тогда
				ОформлениеСтроки.Ячейки.ЦенаБезНДСПодтвердили.ЦветФона = ЦветКрасный;
			Иначе
				ОформлениеСтроки.Ячейки.ЦенаБезНДСПодтвердили.ЦветФона = ЦветЖелтый;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ДанныеСтроки.ЦенаСНДСЗаказали = ДанныеСтроки.ЦенаСНДСПодтвердили Тогда
			Если ДанныеСтроки.ЦенаСНДСПодтвердили = 0 Тогда
				ОформлениеСтроки.Ячейки.ЦенаСНДСПодтвердили.ЦветФона = ЦветКрасный;
			Иначе
				ОформлениеСтроки.Ячейки.ЦенаСНДСПодтвердили.ЦветФона = ЦветЖелтый;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаСравненияКоличествоПриИзменении(Элемент)
	
	НужноПеревыставитьЗаказ = Ложь;
	
	Для каждого Стр ИЗ ТаблицаСравнения Цикл
		Если НЕ Стр.Количество = Стр.КоличествоПодтвердили Тогда
			
			НужноПеревыставитьЗаказ = Истина;
			Прервать;
			
		КонецЕсли;
	КонецЦикла;
	
	КнопкаПринять = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Принять;
	
	Если НужноПеревыставитьЗаказ Тогда
		КнопкаПринять.Текст = "Перевыставить заказ";
		КнопкаПринять.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаВвестиНаОсновании");
	Иначе
		КнопкаПринять.Текст = "Принять уточнение заказа";
		КнопкаПринять.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаГалочка");
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаСравненияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаСравненияПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаСравненияШапкаСообщенияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если Не ТаблицаСравненияШапкаСообщения.Итог("ЕстьРасхождения") = 0 Тогда
	
		ЦветКрасный = WebЦвета.СветлоРозовый;
		ЦветЖелтый	= WebЦвета.СветлоЗолотистый;
		
		Если ДанныеСтроки.ЕстьРасхождения Тогда
			Если ДанныеСтроки.Подтвердили = Дата(1,1,1) или ДанныеСтроки.Подтвердили = Неопределено Тогда
				ОформлениеСтроки.Ячейки.Подтвердили.ЦветФона = ЦветКрасный;
			Иначе
				ОформлениеСтроки.Ячейки.Подтвердили.ЦветФона = ЦветЖелтый;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаСравненияШапкаСообщенияПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаСравненияШапкаСообщенияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры


Процедура ТаблицаСравненияШапкаСообщенияПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаСравненияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Возврат;
	
КонецПроцедуры

Процедура ТаблицаСравненияШапкаСообщенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Возврат;
	
КонецПроцедуры
