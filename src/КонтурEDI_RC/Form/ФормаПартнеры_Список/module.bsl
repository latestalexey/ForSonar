Процедура КоманднаяПанельТСДобавитьТорговуюСеть(Кнопка)
	
	Если ПолучитьВидКомпании() = "Поставщик" Тогда
		
		ПолучитьФорму("ФормаСервис_ПодключениеКТорговымСетям").Открыть();
		
	Иначе
	
		ОткрытьФормуЭлементаСправочника("Партнеры");

	КонецЕсли;
		
КонецПроцедуры

Процедура ОбновитьСписокПартнеров()
	
	СписокПартнеров.Очистить();
	
	Партнеры = ПолучитьСписокЭлементовСправочника("Партнеры");
	
	Для каждого Стр Из Партнеры Цикл
		НоваяСтрока = СписокПартнеров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОбновитьСписокПартнеров();
	ПриОткрытииФормы(ЭтаФорма);
	
	Если РежимВыбораСписка Тогда
		
		КнопкиСписка = ЭлементыФормы.КоманднаяПанельТС.Кнопки;
	
		ДействиеКнопки = Новый Действие("КнопкаВыбратьЭлементНажатие");
		
		КоличествоКнопокДо = КнопкиСписка.Количество();
		
		НоваяКнопка = КнопкиСписка.Добавить("Выбрать",ТипКнопкиКоманднойПанели.Действие,"Выбрать", ДействиеКнопки);
		НоваяКнопка.Картинка	=  ЭлементыФормы.КартинкаВыбор.Картинка;
		НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
		
		Для н = 1 по КоличествоКнопокДо Цикл
		
			КнопкиСписка.Сдвинуть(КнопкиСписка[0],КоличествоКнопокДо);

		КонецЦикла;
		
	КонецЕсли;
	
 	Если ПолучитьВидКомпании() = "Поставщик" Тогда

		Заголовок = "Торговые сети, с которыми вы работаете";
		ЭлементыФормы.КоманднаяПанельТС.Кнопки.ДобавитьТорговуюСеть.Текст = " Добавить торговую сеть";
		ЭлементыФормы.КоманднаяПанельТС.Кнопки.УдалитьТорговуюСеть.Текст  = " Удалить торговую сеть";
		
	Иначе
		
		Заголовок = "Поставщики, с которыми вы работаете";
		ЭлементыФормы.КоманднаяПанельТС.Кнопки.ДобавитьТорговуюСеть.Текст = " Добавить поставщика";
		ЭлементыФормы.КоманднаяПанельТС.Кнопки.УдалитьТорговуюСеть.Текст  = " Удалить поставщика";
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ВыбратьЭлементСписка()
	
	ТекСтрока = ЭлементыФормы.СписокПартнеров.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		СсылкаВыбора = ТекСтрока.Ссылка;
		
		ЭтаФорма.Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаВыбратьЭлементНажатие(Кнопка)
	
	ВыбратьЭлементСписка();
	
КонецПроцедуры

Процедура ОткрытьФормуТорговойСети()
	
	ТекСтрока = ЭлементыФормы.СписокПартнеров.ТекущиеДанные;
	Если НЕ ТекСтрока = Неопределено Тогда
		
		ОткрытьФормуЭлементаСправочника("Партнеры",ТекСтрока.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПартнерыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимВыбораСписка Тогда
		ВыбратьЭлементСписка();
	Иначе
		ОткрытьФормуТорговойСети();
	КонецЕсли;	
		
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтурEDI_ОбновитьСписокПартнеров" Тогда
		ОбновитьСписокПартнеров();
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельТСОткрыть(Кнопка)
		
	ОткрытьФормуТорговойСети();
	
КонецПроцедуры

Процедура КоманднаяПанельТСУдалить(Кнопка)
	
	ТекСтрока = ЭлементыФормы.СписокПартнеров.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		Если Вопрос("Партнер """+ТекСтрока.Наименование+""" будет удален. Продолжить?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Да Тогда
			
			УдалитьЭлементСправочника("Партнеры",ТекСтрока.Ссылка);
			
			ОбновитьСписокПартнеров();
			
		КонецЕсли;
	КонецЕсли;

	Оповестить("КонтурEDI_ОбновитьСписокПартнеров");
	
КонецПроцедуры
