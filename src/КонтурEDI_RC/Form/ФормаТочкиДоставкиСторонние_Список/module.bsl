
Процедура КоманднаяПанельСпискаКнопкаДобавить(Кнопка)

	ФормаТД = ПолучитьФорму("ФормаТочкиДоставкиСторонние_Элемент");
	
	Если ЗначениеЗаполнено(ВладелецВыбора) Тогда
		ФормаТД.Партнер = ВладелецВыбора;
	КонецЕсли;
	
	ФормаТД.Открыть();
	
КонецПроцедуры

Процедура ОбновитьСписокТочекДоставки()
	
	СписокТочекДоставки.Очистить();
	
	СписокЭлементов = ПолучитьСписокЭлементовСправочника("ТочкиДоставкиСторонние",ВладелецВыбора);
	
	Для каждого Стр Из СписокЭлементов Цикл
		
		НоваяСтрока = СписокТочекДоставки.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОткрытьФормуТочкиДоставки()
	
	ТекСтрока = ЭлементыФормы.СписокТочекДоставки.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
	
		ФормаТД = ПолучитьФорму("ФормаТочкиДоставкиСторонние_Элемент");
		ФормаТД.СсылкаТочкиДоставки = ТекСтрока.Ссылка;
		ФормаТД.Открыть();
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОбновитьСписокТочекДоставки();
	
	Если РежимВыбораСписка Тогда
		
		КнопкиСписка = ЭлементыФормы.КоманднаяПанельСписка.Кнопки;
	
		ДействиеКнопки = Новый Действие("КнопкаВыбратьЭлементНажатие");
		
		КоличествоКнопокДо = КнопкиСписка.Количество();
		
		НоваяКнопка = КнопкиСписка.Добавить("Выбрать",ТипКнопкиКоманднойПанели.Действие,"Выбрать", ДействиеКнопки);
		НоваяКнопка.Картинка	=  ЭлементыФормы.КартинкаВыбор.Картинка;
		НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
		
		Для н = 1 по КоличествоКнопокДо Цикл
		
			КнопкиСписка.Сдвинуть(КнопкиСписка[0],КоличествоКнопокДо);

		КонецЦикла;
		
	КонецЕсли;
	
	ПриОткрытииФормы(ЭтаФорма);
	
КонецПроцедуры

Процедура КнопкаВыбратьЭлементНажатие(Кнопка)
	
	ВыбратьЭлементСписка();
	
КонецПроцедуры

Процедура ВыбратьЭлементСписка()
	
	ТекСтрока = ЭлементыФормы.СписокТочекДоставки.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		СсылкаВыбора = ТекСтрока.Ссылка;
		
		ЭтаФорма.Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокТочекДоставкиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимВыбораСписка Тогда
		ВыбратьЭлементСписка();
	Иначе
		ОткрытьФормуТочкиДоставки();
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтурEDI_ОбновитьСписокТочекДоставки" Тогда
		ОбновитьСписокТочекДоставки();
	КонецЕсли;
	
КонецПроцедуры
