Перем ЮрФизЛицо Экспорт;
Перем Сообщение Экспорт;
Перем ИмяПоляСообщения Экспорт;
Перем КтоМы;

Функция ОчиститьПометкиПолей()
	
	ЭлементыФормы.ПометкаGLN.Видимость = Ложь;
	ЭлементыФормы.ПометкаБанк.Видимость = Ложь;
	ЭлементыФормы.ПометкаБик.Видимость = Ложь;
	ЭлементыФормы.ПометкаГлавныйБухгалтер.Видимость = Ложь;
	ЭлементыФормы.ПометкаГород.Видимость = Ложь;
	ЭлементыФормы.ПометкаДом.Видимость = Ложь;
	ЭлементыФормы.ПометкаИмя.Видимость = Ложь;
	ЭлементыФормы.ПометкаИндекс.Видимость = Ложь;
	ЭлементыФормы.ПометкаИНН.Видимость = Ложь;
	ЭлементыФормы.ПометкаКвартира.Видимость = Ложь;
	ЭлементыФормы.ПометкаКорпус.Видимость = Ложь;
	ЭлементыФормы.ПометкаКПП.Видимость = Ложь;
	ЭлементыФормы.ПометкаНаименование.Видимость = Ложь;
	ЭлементыФормы.ПометкаНаселенныйПункт.Видимость = Ложь;
	ЭлементыФормы.ПометкаНомерСчета.Видимость = Ложь;
	ЭлементыФормы.ПометкаРайон.Видимость = Ложь;
	ЭлементыФормы.ПометкаРегион.Видимость = Ложь;
	ЭлементыФормы.ПометкаРуководитель.Видимость = Ложь;
	ЭлементыФормы.ПометкаТелефон.Видимость = Ложь;
	ЭлементыФормы.ПометкаУлица.Видимость = Ложь;
	ЭлементыФормы.ПометкаФамилия.Видимость = Ложь;
	
КонецФункции

Процедура ВывестиПанельИнформации(Текст,Вид = Неопределено)
	
	// Возможные виды:
	//	- Хорошо
	//	- Плохо
	//	- Нейтрально
	
	Если Вид = "Хорошо" Тогда
		
		ЭлементыФормы.ПанельИнформации.ЦветФона		= WebЦвета.Роса;
		ЭлементыФормы.ПанельИнформации.ЦветРамки	= WebЦвета.ЗеленыйЛес;
		
		ЭлементыФормы.ИконкаПанелиИнформации.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаГалочка");
		
	ИначеЕсли Вид = "Плохо" Тогда
		
		ЭлементыФормы.ПанельИнформации.ЦветФона		= WebЦвета.ТусклоРозовый;
		ЭлементыФормы.ПанельИнформации.ЦветРамки	= WebЦвета.Шоколадный;
		
		ЭлементыФормы.ИконкаПанелиИнформации.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаИсправитьОшибки");
		
	Иначе	
		
		ЭлементыФормы.ПанельИнформации.ЦветФона		= WebЦвета.СлоноваяКость;
		ЭлементыФормы.ПанельИнформации.ЦветРамки	= WebЦвета.РыжеватоКоричневый;
		
		ЭлементыФормы.ИконкаПанелиИнформации.Картинка = ПолучитьКартинкуEDI("ЭлементФормы","КартинкаИнформация");
		
	КонецЕсли;
	
	ЭлементыФормы.НадписьИнформация.Заголовок = СокрЛП(Текст);
	
КонецПроцедуры

Процедура ВывестиСведенияОбОшибке(Стр)
	
	НоваяСтрока = ТаблицаОшибок.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
	
	НоваяСтрока.ТекстОшибки = Стр.СведенияОбОшибках;
	
	Если Стр.ИмяПоля = "GLN" Тогда
		
		НоваяСтрока.Действие = "Открыть форму настройки GLN";
		
	Иначе
		
		ИмяПоля = НРег(СокрЛП(ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок));
		ИмяПоля = СтрЗаменить(ИмяПоля,"ент","ента");
		ИмяПоля = СтрЗаменить(ИмяПоля,"ция","ции");
		ИмяПоля = СтрЗаменить(ИмяПоля,"ель","еля");
		
		НоваяСтрока.Действие = "Открыть форму "+ИмяПоля;
		
	КонецЕсли;
	
	НайденноеПоле = ЭлементыФормы.Найти("Пометка"+Стр.ИмяПоля);
	
	Если НЕ НайденноеПоле = Неопределено Тогда
		
		НайденноеПоле.Картинка = ЭлементыФормы.КартинкаОшибка.Картинка;
		НайденноеПоле.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Функция мПроверитьПоляЮрФизЛица()
	
	Если Сообщение.Направление = "Исходящее" Тогда
		
		ОбязательныеПоля 	= ПолучитьОбязательныеПоля(Сообщение);
		ТаблицаОшибокПоля	= ИнициализироватьТаблицуОшибок();
		СтруктураСообщения	= ПолучитьМетаданныеПоляСообщения("ЮрФизЛицо");
		
		ПроверитьПоляСообщенияEDI(ЮрФизЛицо, СтруктураСообщения, ТаблицаОшибокПоля, ИмяПоляСообщения+"EDI",,, ОбязательныеПоля);
		
		БылиОшибки = ?(ТаблицаОшибок.Количество()=0, Ложь, Истина);
		
		ТаблицаОшибок.Очистить();
		
		ОчиститьПометкиПолей();
		
		Если НЕ ТаблицаОшибокПоля.Количество()=0 Тогда
			
			Для Каждого Стр Из ТаблицаОшибокПоля Цикл
				
				Если Стр.ИмяПоля = "Адрес" Тогда
					Для каждого СтрАдреса ИЗ Стр.СведенияОбОшибках Цикл
						
						ВывестиСведенияОбОшибке(СтрАдреса);
						
					КонецЦикла;
					
				Иначе
					ВывестиСведенияОбОшибке(Стр);
				КонецЕсли;	
				
			КонецЦикла;
			
			ВывестиПанельИнформации("В данных по этому юр. лицу найдены ошибки, их необходимо исправить перед отправкой.","Плохо");
			
			ОткрытьПанельОшибок();
			
			Возврат Ложь;
			
		Иначе
			
			Если БылиОшибки Тогда
				
				ВывестиПанельИнформации(" Поздравляем! Все ошибки исправлены!","Хорошо");
				
			Иначе
				
				Если КтоМы = "Поставщик" Тогда
					ОкончаниеИсходящее	= "в торговую сеть.";
					ОкончаниеВходящее	= "от торговой сети.";
				Иначе
					ОкончаниеИсходящее	= "поставщику.";
					ОкончаниеВходящее	= "от поставщика.";
				КонецЕсли;
				
				Если Сообщение.Направление = "Исходящее" Тогда
					
					ВывестиПанельИнформации("В этой форме вы можете проверить данные, которые будут отправлены "+ОкончаниеИсходящее);
					
				Иначе
					
					ВывестиПанельИнформации("В этом окне вы можете увидеть подробно те данные, которые были отправлены "+ОкончаниеВходящее);
					
				КонецЕсли;
				
			КонецЕсли;
			
			ЗакрытьПанельОшибок();
			
		КонецЕсли;
		
	Иначе
		
		Если НЕ ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
			
			ИмяПоля = НРег(СокрЛП(ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок));
			ИмяПоля = СтрЗаменить(ИмяПоля,"ент","ента"); 
			ИмяПоля = СтрЗаменить(ИмяПоля,"ция","цию"); 
			ИмяПоля = СтрЗаменить(ИмяПоля,"ель","еля"); 
			
			ВывестиПанельИнформации("Для загрузки сообщения необходимо выбрать "+ИмяПоля+".");
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьПоляФормы(ЮрФизЛицо)
	
	ВидЮрФизЛица = ЮрФизЛицо.Вид;
	
	Наименование	= ЮрФизЛицо.Наименование;
	Фамилия			= ЮрФизЛицо.Фамилия;
	Имя				= ЮрФизЛицо.Имя;
	Отчество		= ЮрФизЛицо.Отчество;
	
	Если ЗначениеЗаполнено(ЮрФизЛицо.GLN) Тогда
		GLN = ЮрФизЛицо.GLN;
	КонецЕсли;
	ИНН = ЮрФизЛицо.ИНН;
	КПП = ЮрФизЛицо.КПП;
	
	Банк		= ЮрФизЛицо.Банк;
	БИК			= ЮрФизЛицо.БИК;
	НомерСчета	= ЮрФизЛицо.НомерСчета;
	
	Телефон = ЮрФизЛицо.Телефон;
	
	Руководитель		= ЮрФизЛицо.Руководитель;
	ГлавныйБухгалтер	= ЮрФизЛицо.ГлавныйБухгалтер;
	
	Если Не ЗначениеЗаполнено(ЮрФизЛицо.Адрес) Тогда
		
		ВидАдресаЮрФизЛица = "Российский";
		
	Иначе
		
		ВидАдреса = ЮрФизЛицо.Адрес.ВидАдреса;
		
		Если НЕ ЗначениеЗаполнено(ВидАдреса) Тогда
			ВидАдресаЮрФизЛица = "Российский";
		Иначе
			ВидАдресаЮрФизЛица = ВидАдреса;
		КонецЕсли;
		
		АдресЮрФизЛица = ЮрФизЛицо.Адрес;
		
		Индекс			= АдресЮрФизЛица.Индекс;
		Регион			= АдресЮрФизЛица.Регион;
		Район			= АдресЮрФизЛица.Район;
		Город			= АдресЮрФизЛица.Город;
		НаселенныйПункт = АдресЮрФизЛица.НаселенныйПункт;
		Улица			= АдресЮрФизЛица.Улица;
		Дом				= АдресЮрФизЛица.Дом;
		Корпус			= АдресЮрФизЛица.Корпус;
		Квартира		= АдресЮрФизЛица.Квартира;
		
		КодСтраны		= АдресЮрФизЛица.КодСтраны;
		Адрес			= АдресЮрФизЛица.Адрес;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОчиститьПометкиПолей();
	
	КтоМы = ОпределитьКемМыЯвляемся(Сообщение);
	
	Если ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения) Тогда
		Если ИмяПоляСообщения = "Покупатель" Тогда
			Если КтоМы = "Поставщик" Тогда
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Контрагент";
			Иначе
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Организация";
			КонецЕсли;
		ИначеЕсли ИмяПоляСообщения = "Продавец" Тогда
			Если КтоМы = "Поставщик" Тогда
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Организация";
			Иначе
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Контрагент";
			КонецЕсли;
		Иначе
			ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = ИмяПоляСообщения;
		КонецЕсли;
	Иначе
		Если ИмяПоляСообщения = "Покупатель" Тогда
			Если КтоМы = "Поставщик" Тогда
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Организация";
			Иначе
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Контрагент";
			КонецЕсли;
		ИначеЕсли ИмяПоляСообщения = "Продавец" Тогда
			Если КтоМы = "Поставщик" Тогда
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Контрагент";
			Иначе
				ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Организация";
			КонецЕсли;
		Иначе
			ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = ИмяПоляСообщения;
		КонецЕсли;
	КонецЕсли;

	
	ЭлементыФормы.Рамка1С.Заголовок = СокрЛП(ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок)+" в 1С";
	
	Если КтоМы = "Поставщик" Тогда
		Если Сообщение.Направление = "Входящее" Тогда
			ЭлементыФормы.РамкаПолейEDI.Заголовок = "Данные, которые пришли от торговой сети";
		Иначе
			ЭлементыФормы.РамкаПолейEDI.Заголовок = "Данные, которые будут отправлены в торговую сеть";
		КонецЕсли;
	Иначе
		Если Сообщение.Направление = "Входящее" Тогда
			ЭлементыФормы.РамкаПолейEDI.Заголовок = "Данные, которые пришли от поставщика";
		Иначе
			ЭлементыФормы.РамкаПолейEDI.Заголовок = "Данные, которые будут отправлены поставщику";
		КонецЕсли;
	КонецЕсли;
	
	ЮрФизЛицо = Сообщение[ИмяПоляСообщения+"EDI"];
	
	ЮрФизЛицо1С = Сообщение[ИмяПоляСообщения+"1С"];
	
	ЗаполнитьПоляФормы(ЮрФизЛицо);
	
	УстановитьВидимостьПолейЮрФизЛица();
	
	НетОшибок = Истина;
	
	Если Сообщение.Направление = "Исходящее" Тогда
		
		ЭлементыФормы.ЮрФизЛицо1С.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	НетОшибок = мПроверитьПоляЮрФизЛица();
	
	ПриОткрытииФормы(ЭтаФорма);
	
	Если КтоМы = "Поставщик" Тогда
		Если Сообщение.Направление = "Входящее" Тогда
			Если ИмяПоляСообщения = "Покупатель" Тогда
				
				Если НЕ ПараметрыПользователяEDI.ГрузополучательИзЮрФизЛицаТД = Истина Тогда
					
					ТочкаДоставки = ПолучитьЭлементСправочника("ТочкиДоставкиСторонние",Сообщение.Грузополучатель1С);
					
					Если ЗначениеЗаполнено(ТочкаДоставки) И ЗначениеЗаполнено(ТочкаДоставки.ЮрФизЛицо) Тогда
						
						ЭлементыФормы.ЮрФизЛицо1С.Доступность = Ложь;
						ЭлементыФормы.НадписьПримечаниеТочкиДоставки.Видимость = Истина;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Ширина = 923;
	Если НетОшибок Тогда
		
		ЭлементыФормы.ПанельОшибок.Видимость = Ложь;
		ЗакрытьПанельОшибок();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВидЮрФизЛицаПриИзменении(Элемент)
	
	УстановитьВидимостьПолейЮрФизЛица();
	
КонецПроцедуры

Процедура УстановитьВидимостьПолейЮрФизЛица()
	
	Если ВидЮрФизЛица = "ФизЛицо" Тогда
		
		ЭлементыФормы.ПанельНаименования.ТекущаяСтраница = ЭлементыФормы.ПанельНаименования.Страницы.СтраницаФизЛицо;
		
		ЭлементыФормы.НадписьКПП.Видимость	= Ложь;
		ЭлементыФормы.КПП.Видимость			= Ложь;
		
	Иначе
		
		ЭлементыФормы.ПанельНаименования.ТекущаяСтраница = ЭлементыФормы.ПанельНаименования.Страницы.СтраницаЮрЛицо;
		
		ЭлементыФормы.НадписьКПП.Видимость	= Истина;
		ЭлементыФормы.КПП.Видимость			= Истина;
		
	КонецЕсли;		
	
КонецПроцедуры

Процедура ПереключательАдресаПриИзменении(Элемент)
	
	Если ВидАдресаЮрФизЛица = "Российский" Тогда
		
		ЭлементыФормы.ПанельАдреса.ТекущаяСтраница = ЭлементыФормы.ПанельАдреса.Страницы.СтраницаАдресРоссийский;
		
	Иначе
		
		ЭлементыФормы.ПанельАдреса.ТекущаяСтраница = ЭлементыФормы.ПанельАдреса.Страницы.СтраницаАдресИностранный;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьПанельОшибок()
	
	ЭлементыФормы.ПанельОшибок.Свертка = РежимСверткиЭлементаУправления.Лево;
	
	//Ширина = 612;
	
КонецПроцедуры

Процедура ОткрытьПанельОшибок()
	
	//Ширина = 923;
	
	ЭлементыФормы.ПанельОшибок.Видимость = Истина;
	ЭлементыФормы.ПанельОшибок.Свертка = РежимСверткиЭлементаУправления.Нет;
	
КонецПроцедуры

Процедура ПолеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОсновныеДействияФормыВыполнить(Кнопка)
	
	Если Сообщение.Направление = "Входящее" Тогда
		
		Если НЕ ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
			Предупреждение("Не указан "+НРег(ИмяПоляСообщения)+"!");
			Возврат;
		КонецЕсли;
		
		СохранятьСоответствие = Истина;
		
		Если ИмяПоляСообщения = "Грузополучатель" Тогда
			Если КтоМы = "Поставщик" Тогда
				
				ТочкаДоставки = ПолучитьЭлементСправочника("ТочкиДоставкиСторонние",ЮрФизЛицо1С);
				
				Если ЗначениеЗаполнено(ТочкаДоставки.ЮрФизЛицо) Тогда
					СохранятьСоответствие = Ложь;
				КонецЕсли;
				
				Если НЕ СокрЛП(ТочкаДоставки.GLN) = СокрЛП(GLN) Тогда
					ВывестиПредупреждение_КонтурEDI("GLN выбранной точки доставки ("+СокрЛП(ТочкаДоставки.GLN)+") отличается от GLN, отправленного из торговой сети!");
					Возврат;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Сообщение[ИмяПоляСообщения+"1С"] = ЮрФизЛицо1С;
		
		Если СохранятьСоответствие Тогда
			
			МетаданныеСообщения = ПолучитьМетаданныеСообщения(Сообщение.ТипСообщения,Сообщение.Направление);
			СвойстваПоля = ПолучитьСвойстваПоля(ИмяПоляСообщения,МетаданныеСообщения);
			
			Если СвойстваПоля.ВидСтруктурыEDI = Неопределено Тогда
				ТипEDI = СвойстваПоля.ТипEDI;
			Иначе
				ТипEDI = СвойстваПоля.ВидСтруктурыEDI;
			КонецЕсли;
			
			УстановитьСоответствиеПоляШапки(Сообщение[ИмяПоляСообщения+"1С"],Сообщение[ИмяПоляСообщения+"EDI"],СвойстваПоля.Тип1С,ТипEDI);
			
		КонецЕсли;
		
	Иначе
		
		Сообщение[ИмяПоляСообщения+"EDI"] = ЮрФизЛицо;
		
	КонецЕсли;
	
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

Процедура ПометкаНажатие(Элемент)
	
	ИмяПометки = Элемент.Имя;
	
	ИмяПоля = СтрЗаменить(ИмяПометки,"Пометка","");
	
	НайденнаяСтрока = ТаблицаОшибок.Найти(ИмяПоля,"ИмяПоля");
	Если Не НайденнаяСтрока = Неопределено Тогда
		
		ЭлементыФормы.ТаблицаОшибок.ТекущаяСтрока = НайденнаяСтрока;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТаблицаОшибокПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.ТаблицаОшибок.ТекущиеДанные;
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		
		НайденныйЭлемент = ЭлементыФормы.Найти(ТекущаяСтрока.ИмяПоля);
		Если НЕ НайденныйЭлемент = Неопределено Тогда
			
			ЭтаФорма.ТекущийЭлемент = НайденныйЭлемент;
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЮрФизЛицо1СОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
		
		Если (НЕ ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения) И ИмяПоляСообщения = "Грузополучатель") ИЛИ (ИмяПоляСообщения = "Грузоотправитель" И ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения)) Тогда
			Если Сообщение.Направление = "Входящее" Тогда
				Если КтоМы = "Поставщик" Тогда
					
					ФормаТД = ПолучитьФорму("ФормаТочкиДоставкиСторонние_Элемент");
					ФормаТД.СсылкаТочкиДоставки = ЮрФизЛицо1С;
					ФормаТД.Открыть();
					
					Возврат;
					
				КонецЕсли;				
			КонецЕсли;
		КонецЕсли;
		
		ЮрФизЛицо1С.ПолучитьФорму().ОткрытьМодально();
		
		ОбновитьПоляЮрФизЛица();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПоляЮрФизЛица()
	
	Если НЕ ЗначениеЗаполнено(ЮрФизЛицо1С) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеСообщения = ПолучитьМетаданныеСообщения(Сообщение.ТипСообщения,Сообщение.Направление);
	
	НайденноеПоле = МетаданныеСообщения.Найти(ИмяПоляСообщения,"ИмяПоля");
	
	Если НЕ НайденноеПоле = Неопределено Тогда
		
		ТипПоля1С = НайденноеПоле.Тип1С;
		
		ТекGLN = GLN;
		
		ЮрФизЛицо = КонвертироватьЗначение1СвEDI(ЮрФизЛицо1С,ТипПоля1С,"ЮрФизЛицо",Сообщение);
		
		Если НЕ ЗначениеЗаполнено(ЮрФизЛицо.GLN) Тогда
			ЮрФизЛицо.GLN = ТекGLN;
		КонецЕсли;
		
		ЗаполнитьПоляФормы(ЮрФизЛицо);
		
		мПроверитьПоляЮрФизЛица();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЮрФизЛицо1СПриИзменении(Элемент)
	
	мПроверитьПоляЮрФизЛица();
	
КонецПроцедуры

Процедура ТаблицаОшибокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Колонка.Имя = "Действие" Тогда
		Если ЗначениеЗаполнено(ВыбраннаяСтрока.Действие) Тогда
			
			Если Найти(ВыбраннаяСтрока.Действие,"GLN") > 0 Тогда
				
				Если ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Организация" Тогда
					
					ПолучитьФорму("ФормаСтруктураКомпании_Элемент").ОткрытьМодально();
					
				ИначеЕсли ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Контрагент" Тогда
					
					Партнер = ОпределитьПартнера(Новый Структура("ТипЗначения,ЮрФизЛицо","ЮрФизЛицо",ЮрФизЛицо1С));
					
					Если НЕ Партнер = Неопределено Тогда
						
						ОткрытьФормуЭлементаСправочника("Партнеры",Партнер);
						
					Иначе
						
						ПолучитьФорму("ФормаПартнеры_Список").ОткрытьМодально();
						
					КонецЕсли;
					
				ИначеЕсли ЭлементыФормы.НадписьЮрФизЛицо1С.Заголовок = "Грузополучатель" Тогда
					
					Если Сообщение.ТипСообщения = "ORDERS" Тогда
						
						
						ФормаСтруктурыКомпании = ПолучитьФорму("ФормаСтруктураКомпании_Элемент");
						ФормаСтруктурыКомпании.ТекущаяВкладка = "СтраницаТочкиДоставки";
						ФормаСтруктурыКомпании.ОткрытьМодально();
						
						
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				ЮрФизЛицо1С.ПолучитьФорму().ОткрытьМодально();
				
			КонецЕсли;
			
			ОбновитьПоляЮрФизЛица();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЮрФизЛицо1СНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Если (НЕ ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения) И ИмяПоляСообщения = "Грузополучатель") ИЛИ (ИмяПоляСообщения = "Грузоотправитель" И ЭтоОбратнаяОтгрузка(Сообщение.ТипСообщения)) Тогда
		Если Сообщение.Направление = "Входящее" Тогда
			Если КтоМы = "Поставщик" Тогда
				
				ВыбратьЭлементСправочника(ЮрФизЛицо1С,"ТочкаДоставкиСторонняя",Сообщение.Отправитель1С,СтандартнаяОбработка);
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
