Процедура КоманднаяПанельСпискаКнопкаДобавить(Кнопка)
	
	ФормаТД = ПолучитьФорму("ФормаУчетныеЗаписи_Элемент");
	ФормаТД.Открыть();
	
КонецПроцедуры

Процедура ОбновитьСписокУчетныхЗаписей()
	
	УчетнаяЗаписьПоУмолчанию = ПолучитьКонстантуEDI("УчетнаяЗаписьПоУмолчанию");
	
	СписокУчетныхЗаписей.Очистить();
	
	УчетныеЗаписи = ПолучитьСписокЭлементовСправочника("УчетныеЗаписи");
		
	Для каждого Стр ИЗ УчетныеЗаписи Цикл
			
		НоваяСтрока = СписокУчетныхЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);
		
		Если Стр.Ссылка = УчетнаяЗаписьПоУмолчанию Тогда
			НоваяСтрока.Основная = Истина;
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ПриОткрытииФормы(ЭтаФорма);
	
	ОбновитьСписокУчетныхЗаписей();
	
	Если РежимВыбораСписка Тогда
		
		КнопкиСписка = ЭлементыФормы.КоманднаяПанельСписка.Кнопки;
	
		ДействиеКнопки = Новый Действие("КнопкаВыбратьЭлементНажатие");
		
		КоличествоКнопокДо = КнопкиСписка.Количество();
		
		НоваяКнопка = КнопкиСписка.Добавить("Выбрать",ТипКнопкиКоманднойПанели.Действие,"Выбрать", ДействиеКнопки);
		НоваяКнопка.Картинка	= ЭлементыФормы.КартинкаВыбор.Картинка;
		НоваяКнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
		
		Для н = 1 по КоличествоКнопокДо Цикл
		
			КнопкиСписка.Сдвинуть(КнопкиСписка[0],КоличествоКнопокДо);

		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаВыбратьЭлементНажатие(Элемент)
	
	ВыбратьЭлементСписка();
	
КонецПроцедуры

Процедура ВыбратьЭлементСписка()
	
		ТекСтрока = ЭлементыФормы.СписокУчетныхЗаписей.ТекущиеДанные;
		
		Если НЕ ТекСтрока = Неопределено Тогда
			
			СсылкаВыбора = ТекСтрока.Ссылка;
			
			ЭтаФорма.Закрыть(Истина);
			
		КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуУчетнойЗаписи()
	
	ТекСтрока = ЭлементыФормы.СписокУчетныхЗаписей.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
	
		ФормаТД = ПолучитьФорму("ФормаУчетныеЗаписи_Элемент",ЭтаФорма);
		ФормаТД.Ссылка = ТекСтрока.Ссылка;
		ФормаТД.Открыть();
	
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокУчетныхЗаписейВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимВыбораСписка Тогда
		
		ВыбратьЭлементСписка();

	Иначе
		
		ОткрытьФормуУчетнойЗаписи();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтурEDI_ОбновитьСписокУчетныхЗаписей" Тогда
		
		ОбновитьСписокУчетныхЗаписей();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаКнопкаОткрыть(Кнопка)

	ОткрытьФормуУчетнойЗаписи();
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаКнопкаУстановитьОсновной(Кнопка)
	
	Соединение = Неопределено;

	ТекСтрока = ЭлементыФормы.СписокУчетныхЗаписей.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		УстановитьКонстантуEDI("УчетнаяЗаписьПоУмолчанию", ТекСтрока.Ссылка);
		
		ОбновитьСписокУчетныхЗаписей();
		
	КонецЕсли;
		
КонецПроцедуры

Процедура СписокУчетныхЗаписейПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Основная Тогда
		ОформлениеСтроки.Шрифт = Новый Шрифт(ОформлениеСтроки.Шрифт,,,Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокУчетныхЗаписейПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = ЭлементыФормы.СписокУчетныхЗаписей.ТекущиеДанные;
	
	Если НЕ ТекСтрока = Неопределено Тогда
		
		Если ТекСтрока.Основная Тогда
			ЭлементыФормы.КоманднаяПанельСписка.Кнопки.КнопкаУстановитьОсновной.Доступность = Ложь;
		Иначе
			ЭлементыФормы.КоманднаяПанельСписка.Кнопки.КнопкаУстановитьОсновной.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры
