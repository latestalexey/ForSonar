Процедура УстановитьТипПоля1С(ИмяНаФорме,Тип1С)
	
	ПолеФормы	= ЭлементыФормы[ИмяНаФорме];
	ТипПоля		= ПолучитьТипЗначенияОбъекта(Тип1С);
			
	Если ТипПоля = Неопределено Тогда
		
		Сообщить("Не задан тип объекта 1С для поля с типом "+Тип1С);
		
	Иначе	
		
		ЭлементыФормы[ИмяНаФорме].ОграничениеТипа = Новый ОписаниеТипов(ТипПоля);
		
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[ИмяНаФорме]) Тогда
			
			ЭтаФорма[ИмяНаФорме] = ПолучитьПустуюСсылкуОбъекта(Тип1С);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьИзменения()
	
	СтруктураПолей = СоздатьСтруктуруУчетнойЗаписиДляСохранения();

	СохранитьЭлементСправочника("УчетныеЗаписи", Ссылка, СтруктураПолей);
	
	Оповестить("КонтурEDI_ОбновитьСписокУчетныхЗаписей");
		
	Соединение = Неопределено;
	
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)

	Если НЕ ПроверитьЗначенияФормы() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьИзменения();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ПриОткрытииФормы(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		СтруктураЭлемента = ПолучитьЭлементСправочника("УчетныеЗаписи", Ссылка);
		
		Если НЕ СтруктураЭлемента = Неопределено Тогда
		
            Логин									= СтруктураЭлемента.Логин;
			Пароль									= СтруктураЭлемента.Пароль;
			Организация								= СтруктураЭлемента.Организация;
			GLN										= СтруктураЭлемента.GLN;
			Неактивная								= СтруктураЭлемента.Неактивная;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьТипПоля1С("Организация",	"ЮрФизЛицоСвое");
	
	Оповестить("КонтурEDI_АктивизироватьФормуОшибок","Открытие",ЭтаФорма);

	// Автотесты
	Если ЗначениеЗаполнено(ПараметрыАвтотестирования) Тогда

		ПодключитьОбработчикОжидания("ЗаписатьНастройкиАвтотестирования",0.1,Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьНастройкиАвтотестирования()
	
	СтруктураПараметров = ПараметрыАвтотестирования.Настройки.СтруктураКомпании;
	
	Если СтруктураПараметров.УчетныеЗаписи.Количество()>0 Тогда
		
		ТекДанные = СтруктураПараметров.УчетныеЗаписи[0];
		GLN = ТекДанные.GLN;
		Логин = ТекДанные.Логин;
		Пароль = ТекДанные.Пароль;
		
	КонецЕсли;
	
	ЗаписатьИзменения();
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

Функция ПроверитьЗначенияФормы()
	
	// 1. инициализируем таблицу вывода ошибок
	ТаблицаОшибок = ИнициализироватьТаблицуОшибок();
	
	// 2. передать структуру для проверки
	ПроверитьПолеФормы(ТаблицаОшибок, Логин,	"Строка50", Истина, "Логин");
	ПроверитьПолеФормы(ТаблицаОшибок, Пароль, 	"Строка50",	Истина, "Пароль");
	
	Если ТаблицаОшибок.Количество() > 0 Тогда
		
		Если ПустаяСтрока(Логин) Тогда
			
			ТекстЗаголовка = "При заполнении учетной записи найдены ошибки.";
			
		Иначе
			
			ТекстЗаголовка = "При заполнении учетной записи """+СокрЛП(Логин)+""" найдены ошибки.";
			
		КонецЕсли;
			
		ОткрытьФормуВыводаОшибок(ТекстЗаголовка,ТаблицаОшибок,ЭтаФорма);
		
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

Процедура ПриЗакрытии()
	
	Оповестить("КонтурEDI_АктивизироватьФормуОшибок","Закрытие",ЭтаФорма);

КонецПроцедуры

Функция СоздатьСтруктуруУчетнойЗаписиДляСохранения()

	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Логин", 									СокрЛП(Логин));
	СтруктураПолей.Вставить("Пароль", 									Пароль);
	СтруктураПолей.Вставить("Организация", 								Организация);
	СтруктураПолей.Вставить("GLN", 										СокрЛП(GLN));
	СтруктураПолей.Вставить("Неактивная",								Неактивная);
	СтруктураПолей.Вставить("Токен",									"");
	Возврат СтруктураПолей;
	
КонецФункции	
